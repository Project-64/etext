100 REM EDIT LOAD ADDRESS
110 H$="0123456789ABCDEF"
120 PRINT"{clear}EDIT LOAD ADDRESS - 1541"
130 PRINT"{down}REMOVE {rvrs on}WRITE PROTECT TAB{rvrs off}"
140 PRINT"{down}INSERT DISKETTE IN DRIVE"
150 PRINT"{down}PRESS {rvrs on}RETURN{rvrs off} TO CONTINUE"
160 GET C$:IF C$=""THEN 160
170 IF C$<>CHR$(13)GOTO 160
180 PRINT"OK"
190 OPEN 15,8,15
200 PRINT# 15,"I0"
210 INPUT# 15,EN$,EM$,ET$,ES$
220 IF EN$="00"GOTO 260
230 PRINT"{down}"EN$", "EM$","ET$","ES$
240 CLOSE 15
250 END
260 PRINT# 15,"M-R"CHR$(1)CHR$(1)
270 GET#15,DOS$
280 IF DOS$=""THEN DOS$=CHR$(0)
290 DOS=ASC(DOS$)
300 IF DOS=65 GOTO 330
310 PRINT"{down}73,CBM DOS V2.6 1541,00,00"
320 GOTO 910
330 INPUT"{down}FILENAME";F$
340 IF LEN(F$)<>0 AND LEN(F$)<17 GOTO 360
350 GOTO 920
360 OPEN 2,8,2,"0:"+F$+",P,R"
370 INPUT# 15,EN$,EM$,ET$,ES$
380 IF EN$="00"GOTO 400
390 GOTO 940
400 PRINT# 15,"M-R"CHR$(24)CHR$(0)CHR$(2)
410 GET#15,T$
420 T=ASC(T$+CHR$(0))
430 GET#15,S$
440 S=ASC(S$+CHR$(0))
450 CLOSE 2
460 INPUT# 15,EN$,EM$,ET$,ES$
470 IF EN$="00"GOTO 490
480 GOTO 900
490 OPEN 2,8,2,"#2"
500 PRINT# 15,"U1";2;0;T;S
510 INPUT# 15,EN$,EM$,ET$,ES$
520 IF EN$="00"GOTO 540
530 GOTO 900
540 PRINT# 15,"M-R"CHR$(2)CHR$(5)CHR$(2)
550 GET#15,LOW$
560 LOW=ASC(LOW$+CHR$(0))
570 GET#15,HIGH$
580 HIGH=ASC(HIGH$+CHR$(0))
590 D=HIGH
600 GOSUB 1010
610 OLA$=HD$
620 D=LOW
630 GOSUB 1010
640 OLA$=OLA$+HD$
650 PRINT"{down}OLD LOAD ADDRESS: ";OLA$
660 INPUT"{down}NEW LOAD ADDRESS";NLA$
670 IF LEN(NLA$)=4 GOTO 690
680 GOTO 960
690 INPUT"{down}ARE YOU SURE (Y/N)  Y{left:3}";Q$
700 IF Q$<>"Y"GOTO 960
710 HD$=RIGHT$(NLA$,2)
720 GOSUB 1060
730 IF TME=1 GOTO 960
740 LOW=D
750 HD$=LEFT$(NLA$,2)
760 GOSUB 1060
770 IF TME=1 GOTO 960
780 HIGH=D
790 PRINT# 15,"M-W"CHR$(2)CHR$(5)CHR$(2)CHR$(LOW)CHR$(HIGH)
800 PRINT# 15,"U2";2;0;T;S
810 INPUT# 15,EN$,EM$,ET$,ES$
820 IF EN$="00"GOTO 840
830 GOTO 940
840 CLOSE 2
850 INPUT# 15,EN$,EM$,ET$,ES$
860 CLOSE 15
870 PRINT"{down}DONE!"
880 END
890 REM CLOSE
900 PRINT"{down}"EN$", "EM$","ET$","ES$
910 PRINT"{down}{rvrs on}FAILED{rvrs off}"
920 CLOSE 15
930 END
940 PRINT"{down}"EN$", "EM$","ET$","ES$
950 PRINT"{down}{rvrs on}FAILED{rvrs off}"
960 CLOSE 2
970 INPUT# 15,EN$,EM$,ET$,ES$
980 CLOSE 15
990 END
1000 REM DECIMAL TO HEXADECIMAL
1010 H=INT(D/16)
1020 L=D-(H*16)
1030 HD$=MID$(H$,H+1,1)+MID$(H$,L+1,1)
1040 RETURN
1050 REM HEXADECIMAL TO DECIMAL
1060 TME=0
1070 H=0
1080 FOR I=1 TO 16
1090 IF LEFT$(HD$,1)=MID$(H$,I,1)THEN H=I:I=16
1100 NEXT I
1110 IF H=0 THEN TME=1:GOTO 1200
1120 H=H-1
1130 L=0
1140 FOR I=1 TO 16
1150 IF RIGHT$(HD$,1)=MID$(H$,I,1)THEN L=I:I=16
1160 NEXT I
1170 IF L=0 THEN TME=1:GOTO 1200
1180 L=L-1
1190 D=H*16+L
1200 RETURN
