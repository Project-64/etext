100 REM VALIDATE A DISKETTE - 1541
110 CLR
120 CD$="{down:21}"
130 DIM F$(143),T%(143),S%(143)
140 PRINT"{clear}VALIDATE A DISKETTE - 1541"
150 PRINT"{down}INSERT DISKETTE IN DRIVE"
160 PRINT"{down}PRESS {rvrs on}RETURN{rvrs off} TO CONTINUE"
170 GET C$:IF C$=""THEN 170
180 IF C$<>CHR$(13)GOTO 170
190 PRINT"OK"
200 OPEN 15,8,15
210 PRINT# 15,"I0"
220 INPUT# 15,EN$,EM$,ET$,ES$
230 IF EN$="00"GOTO 270
240 PRINT"{down}"EN$", "EM$","ET$","ES$
250 CLOSE 15
260 END
270 PRINT"{down}{rvrs on}FETCHING{rvrs off} DIRECTORY"
280 OPEN 2,8,2,"$0,S,R"
290 INPUT# 15,EN$,EM$,ET$,ES$
300 IF EN$="00"GOTO 320
310 GOTO 240
320 FOR I=0 TO 253
330 GET#2,B$
340 NEXT I
350 N=0
360 FOR J=0 TO 7
370 GET#2,B$
380 IF B$=""THEN B$=CHR$(0)
390 A=ASC(B$)
400 IF A>127 AND A<133 GOTO 510
410 FOR I=0 TO 2
420 GET#2,B$
430 NEXT I
440 IF B$=""THEN B$=CHR$(0)
450 A=ASC(B$)
460 IF A=0 THEN J=7:NEXT J:GOTO 820
470 FOR I=0 TO 25
480 GET#2,B$
490 NEXT I
500 GOTO 750
510 GET#2,B$
520 IF B$=""THEN B$=CHR$(0)
530 T%(N)=ASC(B$)
540 GET#2,B$
550 IF B$=""THEN B$=CHR$(0)
560 S%(N)=ASC(B$)
570 F$=""
580 NULL=0
590 FOR I=0 TO 15
600 GET#2,B$
610 IF B$=""THEN B$=CHR$(0)
620 A=ASC(B$)
630 IF A=0 THEN NULL=NULL+1
640 IF A>127 THEN A=A-128
650 IF A<32 OR A>95 THEN A=63
660 IF A=34 THEN A=63
670 F$=F$+CHR$(A)
680 NEXT I
690 IF NULL=16 THEN J=7:NEXT J:GOTO 820
700 F$(N)=F$
710 N=N+1
720 FOR I=0 TO 10
730 GET#2,B$
740 NEXT I
750 IF J=7 GOTO 790
760 FOR I=0 TO 1
770 GET#2,B$
780 NEXT I
790 NEXT J
800 IF ST=64 GOTO 820
810 GOTO 360
820 CLOSE 2
830 INPUT# 15,EN$,EM$,ET$,ES$
840 IF N>0 GOTO 880
850 PRINT"{down}NO CLOSED FILES ARE IN THE DIRECTORY"
860 CLOSE 15
870 END
880 I=0
890 PRINT"{clear}"
900 N$=RIGHT$("00"+RIGHT$(STR$(N),LEN(STR$(N))-1),3)
910 FOR J=0 TO N-1
920 J$=RIGHT$("00"+RIGHT$(STR$(J+1),LEN(STR$(J+1))-1),3)
930 PRINT"{home}{rvrs on}VALIDATING{rvrs off} #"J$"/"N$":  "F$(J)
940 PRINT"{home}";LEFT$(CD$,I+2);F$(J);" ";
950 NB=1
960 T=T%(J)
970 S=S%(J)
980 GOSUB 1640
990 PRINT"{home}"LEFT$(CD$,I+2)F$(J)NB
1000 JOB=176
1010 GOSUB 1520
1020 IF E=1 GOTO 1040
1030 GOTO 1170
1040 JOB=128
1050 GOSUB 1520
1060 IF E=1 GOTO 1080
1070 GOTO 1170
1080 PRINT# 15,"M-R"CHR$(0)CHR$(4)CHR$(2)
1090 GET#15,B$
1100 T=ASC(B$+CHR$(0))
1110 IF T=0 GOTO 1170
1120 GET#15,B$
1130 S=ASC(B$+CHR$(0))
1140 IF T>35 OR S>20+2*(T>17)+(T>24)+(T>30)THEN I=I+2:R$="{rvrs on}":GOTO 1230
1150 NB=NB+1
1160 GOTO 980
1170 I=I+2
1180 R$="{rvrs off}"
1190 IF E=1 GOTO 1240
1200 R$="{rvrs on}"
1210 GOSUB 1700
1220 GOTO 1250
1230 E$="ILLEGAL TRACK OR SECTOR{left}":GOTO 1250
1240 E$="00, OK,00,00"
1250 PRINT"{home}"R$;LEFT$(CD$,I);F$(J)" "E$"{rvrs off}"
1260 F$(J)="{home}{rvrs on}"+J$+"{rvrs off}"+R$+LEFT$(CD$,I)+"{left:3}"+F$(J)+" "+E$+"{rvrs off}"
1270 IF I=20 AND J<>N-1 THEN FOR D=1 TO 1000:NEXT D:PRINT"{clear}":I=0
1280 NEXT J
1290 CLOSE 15
1300 IF N<11 THEN S=N:GOTO 1500
1310 INPUT"{down}SUMMARY INFORMATION (Y/N)  Y{left:3}";Q$
1320 IF Q$<>"Y"GOTO 110
1330 SI$="{clear}{rvrs on}   /"+N$+"    SUMMARY INFORMATION          {rvrs off}"
1340 S=0
1350 PRINT SI$
1360 FOR I=0 TO 9
1370 PRINT F$(S)
1380 S=S+1
1390 IF S=N THEN I=9
1400 NEXT I
1410 IF S<>N GOTO 1460
1420 IF S=N THEN PRINT"{down}{rvrs on}          TYPE 'C' TO CONTINUE          {rvrs off}"
1430 GET C$:IF C$=""THEN 1430
1440 IF C$<>"C"GOTO 1430
1450 GOTO 110
1460 PRINT"{down}{rvrs on}  TYPE 'C' TO CONTINUE OR 'S' TO STOP   {rvrs off}"
1470 GET C$:IF C$=""THEN 1470
1480 IF C$<>"C"AND C$<>"S"GOTO 1470
1490 IF C$="C"GOTO 1350
1500 GOTO 110
1510 REM JOB QUEUE
1520 TRY=0
1530 PRINT# 15,"M-W"CHR$(8)CHR$(0)CHR$(2)CHR$(T)CHR$(S)
1540 PRINT# 15,"M-W"CHR$(1)CHR$(0)CHR$(1)CHR$(JOB)
1550 TRY=TRY+1
1560 PRINT# 15,"M-R"CHR$(1)CHR$(0)
1570 GET#15,E$
1580 IF E$=""THEN E$=CHR$(0)
1590 E=ASC(E$)
1600 IF TRY=500 GOTO 1620
1610 IF E>127 GOTO 1550
1620 RETURN
1630 REM STR$(T,S)
1640 T$=RIGHT$(STR$(T),LEN(STR$(T))-1)
1650 IF T<10 THEN T$="0"+T$
1660 S$=RIGHT$(STR$(S),LEN(STR$(S))-1)
1670 IF S<10 THEN S$="0"+S$
1680 RETURN
1690 REM EN$,EM$,ET$,ES$
1700 IF E>1 AND E<12 THEN EN$=RIGHT$(STR$(E+18),2):GOTO 1720
1710 EN$="02":EM$="?TIME OUT":GOTO 1730
1720 EM$="READ ERROR"
1730 ET$=T$
1740 ES$=S$
1750 E$=EN$+", "+EM$+","+ET$+","+ES$
1760 RETURN
