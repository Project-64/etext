100 REM RECOVER TRACK & SECTOR - 1541
110 PRINT"{clear}RECOVER TRACK & SECTOR - 1541"
120 PRINT"{down}INSERT DISKETTE IN DRIVE"
130 INPUT"{down}RECOVER TRACK AND SECTOR (T,S)";T,S
140 IF T<1 OR T>35 THEN END
150 NS=20+2*(T>17)+(T>24)+(T>30)
160 IF S<0 OR S>NS THEN END
170 INPUT"{down}ARE YOU SURE  Y{left:3}";Q$
180 IF Q$<>"Y"THEN END
190 OPEN 15,8,15
200 PRINT# 15,"I0"
210 INPUT# 15,EN$,EM$,ET$,ES$
220 IF EN$="00"GOTO 290
230 PRINT"{down}"EN$", "EM$","ET$","ES$
240 PRINT"{down}PRESS {rvrs on}RETURN{rvrs off} TO CONTINUE"
250 GET C$:IF C$=""THEN 250
260 IF C$<>CHR$(13)GOTO 250
270 PRINT"OK"
280 REM SEEK
290 JOB=176
300 GOSUB 520
310 IF E=1 GOTO 340
320 GOTO 640
330 REM READ
340 JOB=128
350 GOSUB 520
360 IF E=4 GOTO 420
370 IF E=5 GOTO 440
380 IF E<>1 GOTO 640
390 PRINT"{down}00, OK,00,00"
400 CLOSE 15
410 END
420 PRINT# 15,"M-W"CHR$(71)CHR$(0)CHR$(1)CHR$(7)
430 REM WRITE
440 JOB=144
450 GOSUB 520
460 IF E=1 GOTO 480
470 GOTO 640
480 CLOSE 15
490 PRINT"{down}DONE!"
500 END
510 REM JOB QUEUE
520 TRY=0
530 PRINT# 15,"M-W"CHR$(8)CHR$(0)CHR$(2)CHR$(T)CHR$(S)
540 PRINT# 15,"M-W"CHR$(1)CHR$(0)CHR$(1)CHR$(JOB)
550 TRY=TRY+1
560 PRINT# 15,"M-R"CHR$(1)CHR$(0)
570 GET#15,E$
580 IF E$=""THEN E$=CHR$(0)
590 E=ASC(E$)
600 IF TRY=500 GOTO 640
610 IF E>127 GOTO 550
620 RETURN
630 REM ERROR HANDLER
640 ET$=RIGHT$(STR$(T),LEN(STR$(T))-1)
650 IF T<10 THEN ET$="0"+ET$
660 ES$=RIGHT$(STR$(S),LEN(STR$(S))-1)
670 IF S<10 THEN ES$="0"+ES$
680 IF E>1 AND E<12 THEN EN$=RIGHT$(STR$(E+18),2):GOTO 700
690 EN$="02":EM$="?TIME OUT":GOTO 720
700 IF E=7 OR E=8 THEN EM$="WRITE ERROR":GOTO 720
710 EM$="READ ERROR"
720 PRINT"{down}"EN$", "EM$","ET$","ES$
730 PRINT"{down}{rvrs on}FAILED{rvrs off}"
740 CLOSE 15
750 END
