100 REM EDIT DISK NAME
110 FOR I=1 TO 16
120 PAD$=PAD$+CHR$(160)
130 NEXT I
140 PRINT"{clear}EDIT DISK NAME - 1541"
150 PRINT"{down}REMOVE {rvrs on}WRITE PROTECT TAB{rvrs off}"
160 PRINT"{down}INSERT DISKETTE IN DRIVE"
170 PRINT"{down}PRESS {rvrs on}RETURN{rvrs off} TO CONTINUE"
180 GET C$:IF C$=""THEN 180
190 IF C$<>CHR$(13)GOTO 180
200 PRINT"OK"
210 OPEN 15,8,15
220 PRINT# 15,"I0"
230 INPUT# 15,EN$,EM$,ET$,ES$
240 IF EN$="00"GOTO 280
250 PRINT"{down}"EN$", "EM$","ET$","ES$
260 CLOSE 15
270 END
280 OPEN 2,8,2,"#"
290 PRINT# 15,"U1";2;0;18;0
300 INPUT# 15,EN$,EM$,ET$,ES$
310 PRINT# 15,"B-P";2;2
320 GET#2,B$
330 IF B$=""THEN B$=CHR$(0)
340 DOS=ASC(B$)
350 IF DOS=65 GOTO 390
360 PRINT"{down}73,CBM DOS V2.6 1541,00,00"
370 PRINT"{down}{rvrs on}FAILED{rvrs off}"
380 GOTO 720
390 PRINT# 15,"B-P";2;144
400 FOR I=1 TO 16
410 GET#2,B$
420 IF B$=""THEN B$=CHR$(0)
430 A=ASC(B$)
440 IF A>127 THEN A=A-128
450 IF A<32 OR A>95 THEN A=63
460 IF A=34 THEN A=63
470 ODN$=ODN$+CHR$(A)
480 NEXT I
490 PRINT"{down}OLD DISK NAME: ";ODN$
500 INPUT"{down}NEW DISK NAME";NDN$
510 IF LEN(NDN$)<>0 AND LEN(NDN$)<17 GOTO 530
520 GOTO 720
530 INPUT"{down}ARE YOU SURE (Y/N)  Y{left:3}";Q$
540 IF Q$<>"Y"GOTO 720
550 NDN$=LEFT$(NDN$+PAD$,16)
560 PRINT# 15,"B-P";2;144
570 PRINT# 2,NDN$;
580 PRINT# 15,"U2";2;0;18;0
590 INPUT# 15,EN$,EM$,ET$,ES$
600 IF EN$="00"GOTO 640
610 PRINT"{down}"EN$", "EM$","ET$","ES$
620 PRINT"{down}{rvrs on}FAILED{rvrs off}"
630 GOTO 720
640 CLOSE 2
650 INPUT# 15,EN$,EM$,ET$,ES$
660 PRINT# 15,"I0"
670 INPUT# 15,EN$,EM$,ET$,ES$
680 CLOSE 15
690 PRINT"{down}DONE!"
700 END
710 REM CLOSE
720 CLOSE 2
730 INPUT# 15,EN$,EM$,ET$,ES$
740 CLOSE 15
750 END
