100 REM EDIT DOS VERSION
110 PRINT"{clear}EDIT DOS VERSION - 1541"
120 PRINT"{down}REMOVE {rvrs on}WRITE PROTECT TAB{rvrs off}"
130 PRINT"{down}INSERT DISKETTE IN DRIVE"
140 PRINT"{down}PRESS {rvrs on}RETURN{rvrs off} TO CONTINUE"
150 GET C$:IF C$=""THEN 150
160 IF C$<>CHR$(13)GOTO 150
170 PRINT"OK"
180 OPEN 15,8,15
190 PRINT# 15,"I0"
200 INPUT# 15,EN$,EM$,ET$,ES$
210 IF EN$="00"GOTO 250
220 PRINT"{down}"EN$", "EM$","ET$","ES$
230 CLOSE 15
240 END
250 PRINT# 15,"M-R"CHR$(1)CHR$(1)
260 GET#15,DOS$
270 IF DOS$=""THEN DOS$=CHR$(0)
280 ODV=ASC(DOS$)
290 PRINT"{down}OLD DOS VERSION:";ODV
300 NDV=-1
310 INPUT"{down}NEW DOS VERSION";NDV
320 IF NDV<0 OR NDV>255 GOTO 500
330 INPUT"{down}ARE YOU SURE (Y/N)  Y{left:3}";Q$
340 IF Q$<>"Y"GOTO 500
350 T=18
360 S=0
370 REM SEEK
380 JOB=176
390 GOSUB 530
400 REM READ
410 JOB=128
420 GOSUB 530
430 PRINT# 15,"M-W"CHR$(2)CHR$(4)CHR$(1)CHR$(NDV)
440 REM WRITE
450 JOB=144
460 GOSUB 530
470 CLOSE 15
480 PRINT"{down}DONE!"
490 END
500 CLOSE 15
510 END
520 REM JOB QUEUE
530 TRY=0
540 PRINT# 15,"M-W"CHR$(8)CHR$(0)CHR$(2)CHR$(T)CHR$(S)
550 PRINT# 15,"M-W"CHR$(1)CHR$(0)CHR$(1)CHR$(JOB)
560 TRY=TRY+1
570 PRINT# 15,"M-R"CHR$(1)CHR$(0)
580 GET#15,E$
590 IF E$=""THEN E$=CHR$(0)
600 E=ASC(E$)
610 IF TRY=500 GOTO 630
620 IF E>127 GOTO 560
630 IF E=1 THEN RETURN
640 CLOSE 15
650 PRINT"{down}{rvrs on}FAILED{rvrs off}"
660 END
