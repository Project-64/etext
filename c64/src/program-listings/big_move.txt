100 PRINT"{clear}{down}   MOVE THE 1541'S READ/WRITE HEAD"
110 PRINT"{down:2}INSERT TEST DISK"
120 PRINT"{down:2}PRESS {rvrs on}RETURN{rvrs off} WHEN READY"
130 :
140 REM  MACHINE CODE ROUTINE TO READ A HEADER
150 REM  RESIDES AT $0300 (BUFFER #0)
160 :
170 DATA 169,48:    :REM LDA #$30
180 DATA 133,69:    :REM STA $45
190 DATA 169,00:    :REM LDA #$00
200 DATA 133,63:    :REM STA $3F
210 DATA 76,177,243 :REM JMP $F3B1
220 :
230 D$(0)="00":D$(1)="01":D$(2)="10":D$(3)="11"
240 DIM FD$(16)
250 FD$(0)="                         "
260 FD$(1)="01 ALL OK                "
270 FD$(2)="02 HEADER BLOCK NOT FOUND"
280 FD$(3)="03 NO SYNC CHARACTER     "
290 FD$(9)="09 HEADER BLOCK CHKSUM ER"
300 T=18:N1$="?":N2$="?":TR=255
310 GET A$:IF A$<>CHR$(13) GOTO 310
320 :
330 OPEN 15,8,15,"I"
340 :
350 REM DIG OUT MASTER DISK ID
360 :
370 PRINT# 15,"M-R"CHR$(18)CHR$(0)CHR$(2)
380 GET#15,I1$:IF I1$=""THEN I1$=CHR$(0)
390 GET#15,I2$:IF I2$=""THEN I2$=CHR$(0)
400 :
410 PRINT"{clear}"
420 :
430 REM READ THE DISK CONTROLLER PORT
440 :
450 PRINT# 15,"M-R"CHR$(0)CHR$(28)
460 GET#15,A$:IF A$=""THEN A$=CHR$(0)
470 A=ASC(A$)
480 CV=3 AND A
490 A=(159 AND A)OR(96+32*((T>17)+(T>24)+(T>30)))
500 PRINT# 15,"M-W"CHR$(0)CHR$(28)CHR$(1)CHR$(A OR 4)
510 :
520 REM DISPLAY VALUES
530 :
540 PRINT"{home}{down}   MOVE THE 1541'S READ/WRITE HEAD"
550 PRINT"{down}CURRENT PHASE ="CV
560 PRINT"BITS 1 & 0 OF $1C00 ARE "D$(CV)
570 PRINT"{down}MASTER DISK ID: "I1$;I2$
580 PRINT"{down}TRACK # FROM STEPPER:"T"{left}    "
590 PRINT"{down}FDC ERROR:"FD$(E)
600 T$=STR$(TR):S$=STR$(SE):IF E<>1 THEN T$="??":N1$="?":N2$="?":S$="??"
610 PRINT"{down}TRACK # AS READ:  "RIGHT$(T$,2)
620 PRINT"SECTOR # AS READ: "RIGHT$(S$,2)
630 PRINT"ID OF TRACK READ: "N1$;N2$
640 PRINT"{down:2}COMMANDS:"
650 PRINT"{down}  F1 = MOVE HEAD OUT (LOWER TRACK #)
660 PRINT"  F3 = MOVE HEAD IN (HIGHER TRACK #)
670 PRINT"  F5 = ATTEMPT TO READ TRACK # & ID"
680 PRINT"  F7 = TERMINATE PROGRAM"
690 PRINT"   I = INITIALIZE (TO TRACK 18)"
700 P=PEEK(197)
710 IF P=3 GOTO 910
720 IF P=4 AND T>1 THEN C=-1:GOTO 800
730 IF P=5 AND T<35 THEN C=1:GOTO 800
740 IF P=6 GOTO 990
750 IF P=33 THEN PRINT# 15,"I":T=18:E=0:A=214:GOTO 480
760 GOTO 450
770 :
780 REM CHANGE PHASE IN RESPONSE TO COMMAND
790 :
800 CV=(CV + C)AND 3
810 T=T+C*.5:IF T<1 THEN T=1
820 IF T>36 THEN T=36
830 B=A AND 252
840 C=B+CV
850 PRINT# 15,"M-W"CHR$(0)CHR$(28)CHR$(1)CHR$(C)
860 E=0
870 GOTO 450
880 :
890 REM TERMINATE PROGRAM (DRIVE OFF)
900 :
910 PRINT# 15,"M-W"CHR$(0)CHR$(28)CHR$(1)CHR$(240)
920 FOR K=1 TO 10:GET A$:NEXT
930 CLOSE 15:END
940 :
950 REM ATTEMPT TO READ ANY HEADER
960 :
970 REM READ & SEND MACHINE CODE ROUTINE
980 :
990 RESTORE:C$=""
1000 FOR K=1 TO 11:READ X:C$=C$+CHR$(X):NEXT
1010 PRINT# 15,"M-W"CHR$(0)CHR$(3)CHR$(11)C$
1020 :
1030 REM PUT JMP JOB IN THE JOB QUEUE
1040 :
1050 PRINT# 15,"M-W"CHR$(0)CHR$(0)CHR$(1)CHR$(208)
1060 :
1070 REM WAIT FOR JOB TO FINISH
1080 :
1090 PRINT# 15,"M-R"CHR$(0)CHR$(0)
1100 GET#15,E$:E=ASC(E$+CHR$(0))
1110 IF E>127 GOTO 790
1120 :
1130 REM "E" IS FDC ERROR CODE RETURNED
1140 IF E<>1 GOTO 450
1150 :
1160 REM CLEAN READ SO DIG OUT ID, TRAK & SECT
1170 :
1180 PRINT# 15,"M-R"CHR$(22)CHR$(0)CHR$(4)
1190 GET#15,N1$
1200 GET#15,N2$
1210 GET#15,X$:TR=ASC(X$+CHR$(0))
1220 GET#15,X$:SE=ASC(X$+CHR$(0))
1230 GOTO 450
