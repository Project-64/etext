100 REM DISPLAY TRACK & SECTOR - 1541
110 CLR
120 HD$="0123456789ABCDEF"
130 PRINT"{clear}DISPLAY TRACK & SECTOR - 1541"
140 PRINT"{down}INSERT DISKETTE IN DRIVE"
150 INPUT"{down}DISPLAY TRACK & SECTOR (T,S)";T,S
160 IF T<1 OR T>35 THEN END
170 NS=20+2*(T>17)+(T>24)+(T>30)
180 IF S<0 OR S>NS THEN END
190 INPUT"{down}OUTPUT TO SCREEN OR PRINTER (S/P)  S{left:3}";O$
200 IF O$<>"S"AND O$<>"P"THEN END
210 INPUT"{down}ARE YOU SURE  Y{left:3}";Q$
220 IF Q$<>"Y"THEN END
230 OPEN 15,8,15
240 T$=RIGHT$(STR$(T),LEN(STR$(T))-1)
250 IF T<10 THEN T$="0"+T$
260 S$=RIGHT$(STR$(S),LEN(STR$(S))-1)
270 IF S<10 THEN S$="0"+S$
280 REM SEEK
290 JOB=176
300 GOSUB 850
310 IF E<>1 GOTO 360
320 REM READ
330 JOB=128
340 GOSUB 850
350 IF E=1 GOTO 470
360 IF E>1 AND E<12 THEN EN$=RIGHT$(STR$(E+18),2):GOTO 380
370 EN$="02":EM$="?TIMEOUT":GOTO 390
380 EM$="READ ERROR"
390 ET$=T$
400 ES$=S$
410 PRINT"{down}"EN$", "EM$","ET$","ES$
420 IF E<>4 AND E<>5 GOTO 450
430 GOSUB 1020
440 GOTO 470
450 CLOSE 15
460 END
470 IF O$="S"GOTO 550
480 OPEN 4,4
490 FOR I=1 TO 6
500 PRINT# 4
510 NEXT I
520 PRINT# 4,"         DISPLAY TRACK & SECTOR"
530 PRINT# 4,"          TRACK "T$" - SECTOR "S$
540 PRINT# 4
550 FOR K=0 TO 1
560 PRINT"{clear}{rvrs on}         DISPLAY TRACK & SECTOR         {rvrs off}"
570 PRINT"{home}{down}{rvrs on}          TRACK "T$" - SECTOR "S$"          {rvrs off}"
580 PRINT"{home}{down:2}"
590 FOR J=0 TO 15
600 D=K*128+J*8
610 GOSUB 970
620 BP$=" . "+DH$+": "
630 H$=""
640 A$=""
650 FOR I=0 TO 7
660 PRINT# 15,"M-R"CHR$(K*128+J*8+I)CHR$(4)
670 GET#15,B$
680 D=ASC(B$+CHR$(0))
690 GOSUB 970
700 H$=H$+DH$+" "
710 IF D>127 THEN D=D-128
720 IF D<32 OR D>90 THEN D=46
730 A$=A$+CHR$(D)
740 NEXT I
750 PRINT BP$;H$;A$
760 IF O$="P"THEN PRINT# 4,BP$;H$;A$
770 NEXT J
780 IF O$="P"GOTO 800
790 GOSUB 1020
800 NEXT K
810 IF O$="P"THEN CLOSE 4
820 CLOSE 15
830 GOTO 110
840 REM JOB QUEUE
850 TRY=0
860 PRINT# 15,"M-W"CHR$(8)CHR$(0)CHR$(2)CHR$(T)CHR$(S)
870 PRINT# 15,"M-W"CHR$(1)CHR$(0)CHR$(1)CHR$(JOB)
880 TRY=TRY+1
890 PRINT# 15,"M-R"CHR$(1)CHR$(0)
900 GET#15,E$
910 IF E$=""THEN E$=CHR$(0)
920 E=ASC(E$)
930 IF TRY=500 GOTO 950
940 IF E>127 GOTO 880
950 RETURN
960 REM DECIMAL TO HEXADECIMAL
970 H=INT(D/16)+1
980 L=D-(H-1)*16+1
990 DH$=MID$(HD$,H,1)+MID$(HD$,L,1)
1000 RETURN
1010 REM DELAY
1020 PRINT"{down}PRESS {rvrs on}RETURN{rvrs off} TO CONTINUE"
1030 GET C$:IF C$=""THEN 1030
1040 IF C$<>CHR$(13)GOTO 1030
1050 PRINT"OK"
1060 RETURN
