100 REM DUPLICATE TRACK & SECTOR - 1541
110 PRINT"{clear}DUPLICATE TRACK & SECTOR - 1541"
120 PRINT"{down}INSERT DISKETTE IN DRIVE"
130 INPUT"{down}SOURCE TRACK AND SECTOR (T,S)";T,S
140 GOSUB 580
150 TR=T:T=0
160 SR=S:S=0
170 INPUT"{down}TARGET TRACK AND SECTOR (T,S)";T,S
180 GOSUB 580
190 TW=T
200 SW=S
210 INPUT"{down}ARE YOU SURE  Y{left:3}";Q$
220 IF Q$<>"Y"THEN END
230 OPEN 15,8,15
240 PRINT# 15,"I0"
250 INPUT# 15,EN$,EM$,ET$,ES$
260 IF EN$="00"GOTO 310
270 PRINT"{down}"EN$", "EM$","ET$","ES$
280 CLOSE 15
290 END
300 REM SEEK
310 T=TR
320 S=SR
330 JOB=176
340 GOSUB 630
350 IF E=1 GOTO 380
360 GOTO 750
370 REM READ
380 JOB=128
390 GOSUB 630
400 IF E=1 GOTO 430
410 GOTO 750
420 REM SEEK
430 T=TW
440 S=SW
450 JOB=176
460 GOSUB 630
470 IF E=1 GOTO 500
480 GOTO 750
490 REM WRITE
500 JOB=144
510 GOSUB 630
520 IF E=1 GOTO 540
530 GOTO 750
540 CLOSE 15
550 PRINT"{down}DONE!"
560 END
570 REM ILLEGAL TRACK OR SECTOR
580 IF T<1 OR T>35 THEN END
590 NS=20+2*(T>17)+(T>24)+(T>30)
600 IF S<0 OR S>NS THEN END
610 RETURN
620 REM JOB QUEUE
630 TRY=0
640 PRINT# 15,"M-W"CHR$(8)CHR$(0)CHR$(2)CHR$(T)CHR$(S)
650 PRINT# 15,"M-W"CHR$(1)CHR$(0)CHR$(1)CHR$(JOB)
660 TRY=TRY+1
670 PRINT# 15,"M-R"CHR$(1)CHR$(0)
680 GET#15,E$
690 IF E$=""THEN E$=CHR$(0)
700 E=ASC(E$)
710 IF TRY=500 GOTO 750
720 IF E>127 GOTO 660
730 RETURN
740 REM ERROR HANDLER
750 ET$=RIGHT$(STR$(T),LEN(STR$(T))-1)
760 IF T<10 THEN ET$="0"+ET$
770 ES$=RIGHT$(STR$(S),LEN(STR$(S))-1)
780 IF S<10 THEN ES$="0"+ES$
790 IF E>1 AND E<12 THEN EN$=RIGHT$(STR$(E+18),2):GOTO 810
800 EN$="02":EM$="?TIME OUT":GOTO 830
810 IF E=7 OR E=8 THEN EM$="WRITE ERROR":GOTO 830
820 EM$="READ ERROR"
830 PRINT"{down}"EN$", "EM$","ET$","ES$
840 PRINT"{down}{rvrs on}FAILED{rvrs off}"
850 CLOSE 15
860 END
