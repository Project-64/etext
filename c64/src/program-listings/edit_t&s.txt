100 REM EDIT TRACK & SECTOR - 1541
110 POKE 56,159
120 CLR
130 HD$="0123456789ABCDEF"
140 CD$="{home}{down:20}"
150 PRINT"{clear}EDIT A SECTOR - 1541"
160 PRINT"{down}REMOVE {rvrs on}WRITE PROTECT TAB{rvrs off}"
170 PRINT"{down}INSERT DISKETTE IN DRIVE"
180 INPUT"{down}EDIT TRACK & SECTOR (T,S)";T,S
190 IF T<1 OR T>35 GOTO 1580
200 NS=20+2*(T>17)+(T>24)+(T>30)
210 IF S<0 OR S>NS GOTO 1580
220 INPUT"{down}STARTING BYTE (00/80)";SB$
230 IF LEN(SB$)=0 GOTO 1580
240 SB=VAL(SB$)
250 IF SB<>0 AND SB<>80 GOTO 1580
260 IF SB=0 THEN BP=0:GOTO 280
270 BP=128
280 INPUT"{down}ARE YOU SURE  Y{left:3}";Q$
290 IF Q$<>"Y"GOTO 1580
300 OPEN 15,8,15
310 T$=RIGHT$(STR$(T),LEN(STR$(T))-1)
320 IF T<10 THEN T$="0"+T$
330 S$=RIGHT$(STR$(S),LEN(STR$(S))-1)
340 IF S<10 THEN S$="0"+S$
350 REM SEEK
360 JOB=176
370 GOSUB 1620
380 IF E<>1 GOTO 430
390 REM READ
400 JOB=128
410 GOSUB 1620
420 IF E=1 GOTO 520
430 IF E>1 AND E<12 THEN EN$=RIGHT$(STR$(E+18),2):GOTO 450
440 EN$="02":EM$="?TIMEOUT":GOTO 470
450 IF E=7 OR E=8 THEN EM$="WRITE ERROR":GOTO 470
460 EM$="READ ERROR"
470 ET$=T$
480 ES$=S$
490 PRINT"{down}"EN$", "EM$","ET$","ES$
500 CLOSE 15
510 GOTO 1580
520 PRINT"{clear}{rvrs on}          EDIT TRACK & SECTOR           {rvrs off}"
530 PRINT"{home}{down}{rvrs on}          TRACK "T$" - SECTOR "S$"          {rvrs off}"
540 PRINT"{home}{down:2}"
550 FOR J=0 TO 15
560 D=J*8+BP
570 GOSUB 1740
580 BP$=" . "+DH$+": "
590 H$=""
600 A$=""
610 FOR I=0 TO 7
620 PRINT# 15,"M-R"CHR$(J*8+I+BP)CHR$(4)
630 GET#15,B$
640 D=ASC(B$+CHR$(0))
650 POKE(40704+J*8+I),D
660 GOSUB 1740
670 H$=H$+DH$+" "
680 IF D>127 THEN D=D-128
690 IF D<32 OR D>95 THEN D=46
700 IF D=34 THEN D=46
710 A$=A$+CHR$(D)
720 NEXT I
730 PRINT BP$H$"{rvrs on}"A$"{rvrs off}"
740 NEXT J
750 PRINT"{down}{rvrs on}EDIT{rvrs off} TRACK "T$" - SECTOR "S$" (Y/N)?"
760 GOSUB 1790
770 IF Q$<>"Y"GOTO 1390
780 PRINT CD$"PRESS {rvrs on}CLR{rvrs off} TO EXIT               "
790 PRINT"{home}{down:3}{right:7}";
800 S=1151
810 C=1
820 A=PEEK(S):IF A>127 THEN A=A-128
830 M=S
840 POKE M,A+128
850 GET I$:IF I$=""THEN 850
860 I=ASC(I$)
870 IF I=147 THEN POKE M,A:GOTO 1360
880 IF I=19 THEN POKE M,A:GOTO 790
890 IF I=141 THEN I=13
900 IF I<>13 GOTO 930
910 IF C=23 AND S<>1773 THEN PRINT"{right}";:GOTO 1230
920 IF S<1751 THEN POKE M,A:FOR I=C TO 23:PRINT"{right}";:S=S+1:NEXT I:S=S-1:C=23:GOTO 1230
930 IF I=32 THEN I=29:I$=CHR$(29)
940 IF I<>29 GOTO 970
950 IF C<>23 THEN C=C+1:S=S+1:GOTO 1290
960 IF S<>1773 THEN PRINT"{right}";:GOTO 1230
970 IF I<>157 GOTO 1000
980 IF C<>1 THEN C=C-1:S=S-1:GOTO 1290
990 IF C=1 AND S<>1151 THEN FOR I=1 TO 18:PRINT"{left}";:NEXT I:C=23:S=S-18:GOTO 1300
1000 IF I<>17 GOTO 1020
1010 IF S+40<1774 THEN S=S+40:GOTO 1290
1020 IF I<>145 GOTO 1040
1030 IF S-40>1150 THEN S=S-40:GOTO 1290
1040 IF A=32 OR A=160 GOTO 850
1050 IF I<48 OR I>57 AND I<65 OR I>70 GOTO 820
1060 PRINT I$;
1070 A=I:IF I>64 THEN A=A-64
1080 IF A<7 THEN L=A+9
1090 IF A>47 THEN L=A-48
1100 IF INT((C+1)/3)=(C+1)/3 THEN R=PEEK(S-1):GOTO 1120
1110 R=PEEK(S+1)
1120 IF R>127 THEN R=R-128
1130 IF R<7 THEN R=R+9
1140 IF R>47 THEN R=R-48
1150 IF INT((C+1)/3)<>(C+1)/3 THEN I=L*16+R:GOTO 1170
1160 I=R*16+L
1170 POKE 40704+8*INT((M-1151)/40)+INT(C/3),I
1180 IF I>127 THEN I=I-128
1190 IF I<32 OR I>95 THEN I=46
1200 IF I=34 THEN I=46
1210 IF I>64 THEN POKE M+25-C+INT(C/3),I-64+128:GOTO 1230
1220 POKE M+25-C+INT(C/3),I+128
1230 IF C=23 AND S<>1773 THEN FOR I=1 TO 17:PRINT"{right}";:NEXT I:C=1:S=S+18:GOTO 1300
1240 IF S=1773 THEN PRINT"{left}";:GOTO 1300
1250 S=S+1
1260 C=C+1
1270 POKE M,A
1280 GOTO 820
1290 PRINT I$;
1300 A=PEEK(M):IF A>127 THEN A=A-128
1310 POKE M,A
1320 GOTO 820
1330 PRINT CD$"EXIT (Y/N)?"
1340 GOSUB 1790
1350 IF Q$="N"GOTO 780
1360 PRINT CD$"{rvrs on}REWRITE{rvrs off} TRACK "T$" - SECTOR "S$" (Y/N)?"
1370 GOSUB 1790
1380 IF Q$="Y"GOTO 1450
1390 CLOSE 15
1400 PRINT CD$"ATTEMPT TO EDIT A SECTOR {rvrs on}FAILED{rvrs off}    "
1410 PRINT"{down}PRESS {rvrs on}RETURN{rvrs off} TO CONTINUE"
1420 GET C$:IF C$=""THEN 1420
1430 IF C$<>CHR$(13)GOTO 1420
1440 GOTO 120
1450 PRINT CD$"{rvrs on}REWRITING{rvrs off} TRACK "T$" - SECTOR "S$"     "
1460 FOR I=0 TO 127
1470 PRINT# 15,"M-W"CHR$(I+BP)CHR$(4)CHR$(1)CHR$(PEEK(40704+I))
1480 NEXT I
1490 REM WRITE
1500 T=VAL(T$)
1510 S=VAL(S$)
1520 JOB=144
1530 GOSUB 1620
1540 CLOSE 15
1550 IF E<>1 GOTO 1400
1560 PRINT CD$"ATTEMPT TO EDIT A SECTOR COMPLETE"
1570 GOTO 1410
1580 POKE 56,160
1590 CLR
1600 END
1610 REM JOB QUEUE
1620 TRY=0
1630 PRINT# 15,"M-W"CHR$(8)CHR$(0)CHR$(2)CHR$(T)CHR$(S)
1640 PRINT# 15,"M-W"CHR$(1)CHR$(0)CHR$(1)CHR$(JOB)
1650 TRY=TRY+1
1660 PRINT# 15,"M-R"CHR$(1)CHR$(0)
1670 GET#15,E$
1680 IF E$=""THEN E$=CHR$(0)
1690 E=ASC(E$)
1700 IF TRY=500 GOTO 1720
1710 IF E>127 GOTO 1650
1720 RETURN
1730 REM DECIMAL TO HEXADECIMAL
1740 H=INT(D/16)+1
1750 L=D-(H-1)*16+1
1760 DH$=MID$(HD$,H,1)+MID$(HD$,L,1)
1770 RETURN
1780 REM QUERY
1790 GET Q$:IF Q$=""THEN 1790
1800 IF Q$<>"Y"AND Q$<>"N"GOTO 1790
1810 RETURN
