100 REM 22A ERROR - 1541
110 PRINT"{clear}22A ERROR - 1541"
120 PRINT"{down}INSERT CLONE IN DRIVE"
130 INPUT"{down}DESTROY TRACK AND SECTOR (T,S)";T,S
140 IF T<1 OR T>35 THEN END
150 NS=20+2*(T>17)+(T>24)+(T>30)
160 IF S<0 OR S>NS THEN END
170 INPUT"{down}ARE YOU SURE  Y{left:3}";Q$
180 IF Q$<>"Y"THEN END
190 OPEN 15,8,15
200 PRINT# 15,"I0"
210 INPUT# 15,EN$,EM$,ET$,ES$
220 IF EN$="00"GOTO 270
230 PRINT"{down}"EN$", "EM$","ET$","ES$
240 CLOSE 15
250 END
260 REM SEEK
270 JOB=176
280 GOSUB 440
290 IF E<>1 GOTO 550
300 REM READ
310 JOB=128
320 GOSUB 440
330 IF E<>1 AND E<>4 AND E<>5 GOTO 550
340 PRINT# 15,"M-W"CHR$(71)CHR$(0)CHR$(1)CHR$(6)
350 REM WRITE
360 JOB=144
370 GOSUB 440
380 PRINT# 15,"M-W"CHR$(71)CHR$(0)CHR$(1)CHR$(7)
390 IF E<>1 GOTO 550
400 CLOSE 15
410 PRINT"{down}DONE!"
420 END
430 REM JOB QUEUE
440 TRY=0
450 PRINT# 15,"M-W"CHR$(8)CHR$(0)CHR$(2)CHR$(T)CHR$(S)
460 PRINT# 15,"M-W"CHR$(1)CHR$(0)CHR$(1)CHR$(JOB)
470 TRY=TRY+1
480 PRINT# 15,"M-R"CHR$(1)CHR$(0)
490 GET#15,E$
500 IF E$=""THEN E$=CHR$(0)
510 E=ASC(E$)
520 IF TRY=500 GOTO 540
530 IF E>127 GOTO 470
540 RETURN
550 CLOSE 15
560 PRINT"{down}{rvrs on}FAILED{rvrs off}"
570 END
