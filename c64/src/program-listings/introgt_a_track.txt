100 REM INTERROGATE A TRACK - 1541
110 PRINT"{clear}INTERROGATE A TRACK - 1541"
120 PRINT"{down}INSERT MASTER IN DRIVE"
130 INPUT"{down}INTERROGATE TRACK";T
140 IF T<1 OR T>35 THEN END
150 INPUT"{down}ARE YOU SURE  Y{left:3}";Q$
160 IF Q$<>"Y"THEN END
170 OPEN 15,8,15
180 NS=20+2*(T>17)+(T>24)+(T>30)
190 REM SEEK
200 JOB=176
210 GOSUB 370
220 REM READ
230 PRINT"{clear}"
240 FOR S=0 TO NS
250 JOB=128
260 GOSUB 370
270 S$=RIGHT$(STR$(S),LEN(STR$(S))-1)
280 IF S<10 THEN S$=" "+S$
290 PRINT"TRACK";T;"- ";
300 IF E=1 THEN PRINT"SECTOR "S$" = OK":GOTO 330
310 IF E>1 AND E<12 THEN EM$=STR$(E+18)+" READ ERROR"
320 PRINT"SECTOR "S$" ="EM$
330 NEXT S
340 CLOSE 15
350 END
360 REM JOB QUEUE
370 TRY=0
380 PRINT# 15,"M-W"CHR$(8)CHR$(0)CHR$(2)CHR$(T)CHR$(S)
390 PRINT# 15,"M-W"CHR$(1)CHR$(0)CHR$(1)CHR$(JOB)
400 TRY=TRY+1
410 PRINT# 15,"M-R"CHR$(1)CHR$(0)
420 GET#15,E$
430 IF E$=""THEN E$=CHR$(0)
440 E=ASC(E$)
450 IF TRY=500 GOTO 480
460 IF E>127 GOTO 400
470 RETURN
480 EM$="?TIME OUT"
490 RETURN
