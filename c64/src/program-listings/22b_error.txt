100 REM DUPLICATE A 22 ERROR - 1541
110 PRINT"{clear}DUPLICATE A 22 ERROR - 1541"
120 PRINT"{down}INSERT MASTER IN DRIVE"
130 INPUT"{down}TRACK AND SECTOR (T,S)";T,S
140 IF T<1 OR T>35 THEN END
150 NS=20+2*(T>17)+(T>24)+(T>30)
160 IF S<0 OR S>NS THEN END
170 INPUT"{down}ARE YOU SURE  Y{left:3}";Q$
180 IF Q$<>"Y"THEN END
190 OPEN 15,8,15
200 PRINT# 15,"I0"
210 INPUT# 15,EN$,EM$,ET$,ES$
220 IF EN$="00"GOTO 270
230 PRINT"{down}"EN$", "EM$","ET$","ES$
240 CLOSE 15
250 END
260 REM SEEK
270 JOB=176
280 GOSUB 550
290 REM READ
300 JOB=128
310 GOSUB 550
320 PRINT# 15,"M-R"CHR$(56)CHR$(0)
330 GET#15,D$
340 IF D$=""THEN D$=CHR$(0)
350 CLOSE 15
360 PRINT"{down}REMOVE MASTER FROM DRIVE"
370 PRINT"INSERT CLONE IN DRIVE"
380 PRINT"PRESS {rvrs on}RETURN{rvrs off} TO CONTINUE"
390 GET C$:IF C$=""THEN 390
400 IF C$<>CHR$(13)GOTO 390
410 PRINT"OK"
420 OPEN 15,8,15
430 REM SEEK
440 JOB=176
450 GOSUB 550
460 PRINT# 15,"M-W"CHR$(71)CHR$(0)CHR$(1)D$
470 REM WRITE
480 JOB=144
490 GOSUB 550
500 PRINT# 15,"M-W"CHR$(71)CHR$(0)CHR$(1)CHR$(7)
510 CLOSE 15
520 PRINT"{down}DONE!"
530 END
540 REM JOB QUEUE
550 TRY=0
560 PRINT# 15,"M-W"CHR$(8)CHR$(0)CHR$(2)CHR$(T)CHR$(S)
570 PRINT# 15,"M-W"CHR$(1)CHR$(0)CHR$(1)CHR$(JOB)
580 TRY=TRY+1
590 PRINT# 15,"M-R"CHR$(1)CHR$(0)
600 GET#15,E$
610 IF E$=""THEN E$=CHR$(0)
620 E=ASC(E$)
630 IF TRY=500 GOTO 660
640 IF E>127 GOTO 580
650 RETURN
660 PRINT# 15,"M-W"CHR$(71)CHR$(0)CHR$(1)CHR$(7)
670 CLOSE 15
680 PRINT"{down}{rvrs on}FAILED{rvrs off}"
690 END
