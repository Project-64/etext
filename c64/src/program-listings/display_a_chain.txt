100 REM DISPLAY A CHAIN - 1541
110 CLR
120 PRINT"{clear}DISPLAY A CHAIN - 1541"
130 PRINT"{down}INSERT DISKETTE IN DRIVE"
140 INPUT"{down}TRACK & SECTOR (T,S)";T,S
150 IF T<1 OR T>35 THEN END
160 NS=20+2*(T>17)+(T>24)+(T>30)
170 IF S<0 OR S>NS THEN END
180 INPUT"{down}OUTPUT TO SCREEN OR PRINTER (S/P)  S{left:3}";O$
190 IF O$<>"S"AND O$<>"P"THEN END
200 INPUT"{down}ARE YOU SURE  Y{left:3}";Q$
210 IF Q$<>"Y"THEN END
220 OPEN 15,8,15
230 PRINT# 15,"I0"
240 INPUT# 15,EN$,EM$,ET$,ES$
250 IF EN$="00"GOTO 290
260 PRINT"{down}"EN$", "EM$","ET$","ES$
270 CLOSE 15
280 END
290 IF O$="S"GOTO 390
300 PRINT"{down}{rvrs on}PRINTING{rvrs off} A CHAIN"
310 OPEN 4,4
320 FOR I=1 TO 6
330 PRINT# 4
340 NEXT I
350 PRINT# 4,"             DISPLAY A CHAIN"
360 PRINT# 4,"         BLOCK   TRACK - SECTOR"
370 PRINT# 4
380 GOTO 420
390 PRINT"{clear}{rvrs on}             DISPLAY A CHAIN            {rvrs off}"
400 PRINT"{home}{down}{rvrs on}         BLOCK   TRACK - SECTOR         {rvrs off}"
410 PRINT"{home}{down:2}"
420 B=B+1
430 GOSUB 1030
440 REM SEEK
450 JOB=176
460 GOSUB 910
470 IF E<>1 GOTO 520
480 REM READ
490 JOB=128
500 GOSUB 910
510 IF E=1 GOTO 630
520 IF E>1 AND E<12 THEN EN$=RIGHT$(STR$(E+18),2):GOTO 540
530 EN$="02":EM$="?TIMEOUT":GOTO 550
540 EM$="READ ERROR"
550 ET$=T$
560 ES$=S$
570 IF O$="P"THEN PRINT# 4,"          "EN$", "EM$","ET$","ES$:GOTO 590
580 PRINT"          "EN$", "EM$","ET$","ES$
590 IF E=4 OR E=5 GOTO 630
600 IF O$="P"GOTO 810
610 GOSUB 1090
620 GOTO 820
630 B$=RIGHT$(STR$(B),LEN(STR$(B))-1)
640 IF B<10 THEN B$=" "+B$
650 IF B<100 THEN B$=" "+B$
660 IF O$="P"THEN PRINT# 4,"          "B$"       "T$" - "S$:GOTO 680
670 PRINT"          "B$"       "T$" - "S$
680 PRINT# 15,"M-R"CHR$(0)CHR$(4)CHR$(2)
690 GET#15,T$
700 T=ASC(T$+CHR$(0))
710 IF T=0 GOTO 760
720 GET#15,S$
730 S=ASC(S$+CHR$(0))
740 IF T>35 OR S>20+2*(T>17)+(T>24)+(T>30)GOTO 850
750 IF O$="S"AND B/16<>INT(B/16)GOTO 420
760 IF O$="P"GOTO 780
770 GOSUB 1090
780 IF T=0 GOTO 810
790 IF O$="S"GOTO 390
800 GOTO 420
810 IF O$="P"THEN CLOSE 4
820 CLOSE 15
830 GOTO 110
840 REM ILLEGAL TRACK OR SECTOR
850 GOSUB 1030
860 IF O$="P"THEN PRINT# 4,"         66, ILLEGAL TRACK OR SECTOR,"T$","S$:GOTO 810
870 PRINT"{down}66, ILLEGAL TRACK OR SECTOR,"T$","S$
880 GOSUB 1090
890 GOTO 820
900 REM JOB QUEUE
910 TRY=0
920 PRINT# 15,"M-W"CHR$(8)CHR$(0)CHR$(2)CHR$(T)CHR$(S)
930 PRINT# 15,"M-W"CHR$(1)CHR$(0)CHR$(1)CHR$(JOB)
940 TRY=TRY+1
950 PRINT# 15,"M-R"CHR$(1)CHR$(0)
960 GET#15,E$
970 IF E$=""THEN E$=CHR$(0)
980 E=ASC(E$)
990 IF TRY=500 GOTO 1010
1000 IF E>127 GOTO 940
1010 RETURN
1020 REM STR$(T,S)
1030 T$=RIGHT$(STR$(T),LEN(STR$(T))-1)
1040 IF T<10 THEN T$="0"+T$
1050 S$=RIGHT$(STR$(S),LEN(STR$(S))-1)
1060 IF S<10 THEN S$="0"+S$
1070 RETURN
1080 REM DELAY
1090 PRINT"{down}PRESS {rvrs on}RETURN{rvrs off} TO CONTINUE"
1100 GET C$:IF C$=""THEN 1100
1110 IF C$<>CHR$(13)GOTO 1100
1120 RETURN
