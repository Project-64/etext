100 REM EDIT DISK ID
110 PRINT"{clear}EDIT DISK ID - 1541"
120 PRINT"{down}REMOVE {rvrs on}WRITE PROTECT TAB{rvrs off}"
130 PRINT"{down}INSERT DISKETTE IN DRIVE"
140 PRINT"{down}PRESS {rvrs on}RETURN{rvrs off} TO CONTINUE"
150 GET C$:IF C$=""THEN 150
160 IF C$<>CHR$(13)GOTO 150
170 PRINT"OK"
180 OPEN 15,8,15
190 PRINT# 15,"I0"
200 INPUT# 15,EN$,EM$,ET$,ES$
210 IF EN$="00"GOTO 250
220 PRINT"{down}"EN$", "EM$","ET$","ES$
230 CLOSE 15
240 END
250 OPEN 2,8,2,"#"
260 PRINT# 15,"U1";2;0;18;0
270 INPUT# 15,EN$,EM$,ET$,ES$
280 PRINT# 15,"B-P";2;2
290 GET#2,B$
300 IF B$=""THEN B$=CHR$(0)
310 DOS=ASC(B$)
320 IF DOS=65 GOTO 360
330 PRINT"{down}73,CBM DOS V2.6 1541,00,00"
340 PRINT"{down}{rvrs on}FAILED{rvrs off}"
350 GOTO 690
360 PRINT# 15,"B-P";2;162
370 FOR I=1 TO 2
380 GET#2,B$
390 IF B$=""THEN B$=CHR$(0)
400 A=ASC(B$)
410 IF A>127 THEN A=A-128
420 IF A<32 OR A>95 THEN A=63
430 IF A=34 THEN A=63
440 ODI$=ODI$+CHR$(A)
450 NEXT I
460 PRINT"{down}OLD DISK ID: ";ODI$
470 INPUT"{down}NEW DISK ID";NDI$
480 IF LEN(NDI$)<>0 AND LEN(NDI$)<3 GOTO 500
490 GOTO 690
500 INPUT"{down}ARE YOU SURE (Y/N)  Y{left:3}";Q$
510 IF Q$<>"Y"GOTO 690
520 NDI$=LEFT$(NDI$+CHR$(0),2)
530 PRINT# 15,"B-P";2;162
540 PRINT# 2,NDI$;
550 PRINT# 15,"U2";2;0;18;0
560 INPUT# 15,EN$,EM$,ET$,ES$
570 IF EN$="00"GOTO 610
580 PRINT"{down}"EN$", "EM$","ET$","ES$
590 PRINT"{down}{rvrs on}FAILED{rvrs off}"
600 GOTO 690
610 CLOSE 2
620 INPUT# 15,EN$,EM$,ET$,ES$
630 PRINT# 15,"I0"
640 INPUT# 15,EN$,EM$,ET$,ES$
650 CLOSE 15
660 PRINT"{down}DONE!"
670 END
680 REM CLOSE
690 CLOSE 2
700 INPUT# 15,EN$,EM$,ET$,ES$
710 CLOSE 15
720 END
