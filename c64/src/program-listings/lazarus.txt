100 REM LAZARUS - 1541
110 PRINT"{clear}LAZARUS - 1541"
120 PRINT"{down}INSERT DISKETTE IN DRIVE"
130 INPUT"{home}{down:4}ATTEMPT A RESURRECTION (Y/N)  Y{left:3}";Q$
140 IF Q$<>"Y"THEN END
150 OPEN 15,8,15
160 REM SEEK
170 FOR T=1 TO 35
180 NS=20+2*(T>17)+(T>24)+(T>30)
190 T$=RIGHT$(STR$(T),LEN(STR$(T))-1)
200 IF T<10 THEN T$="0"+T$
210 JOB=176
220 GOSUB 510
230 IF E=1 GOTO 250
240 BD=BD+1:R=R+NS:GOTO 420
250 REM READ
260 FOR S=0 TO NS
270 S$=RIGHT$(STR$(S),LEN(STR$(S))-1)
280 IF S<10 THEN S$="0"+S$
290 PRINT"{home}{down:6}{rvrs on}RESURRECTING{rvrs off} TRACK "T$" - SECTOR "S$
300 JOB=128
310 GOSUB 510
320 IF E=1 GOTO 410
330 R=R+1
340 IF E<>4 AND E<>5 GOTO 410
350 IF E=5 GOTO 380
360 PRINT# 15,"M-W"CHR$(71)CHR$(0)CHR$(1)CHR$(7)
370 REM WRITE
380 JOB=144
390 GOSUB 510
400 IF E<>1 THEN W=W+1
410 NEXT S
420 NEXT T
430 CLOSE 15
440 PRINT"{home}{down:6}                                  "
450 IF BD=35 THEN PRINT"{home}{down:6}?BAD DISK":END
460 PRINT"{home}{down:6}READ ERRORS :"R
470 PRINT"{down}WRITE ERRORS:"W
480 PRINT"{down}DONE!"
490 END
500 REM JOB QUEUE
510 TRY=0
520 PRINT# 15,"M-W"CHR$(8)CHR$(0)CHR$(2)CHR$(T)CHR$(S)
530 PRINT# 15,"M-W"CHR$(1)CHR$(0)CHR$(1)CHR$(JOB)
540 TRY=TRY+1
550 PRINT# 15,"M-R"CHR$(1)CHR$(0)
560 GET#15,E$
570 IF E$=""THEN E$=CHR$(0)
580 E=ASC(E$)
590 IF TRY=500 GOTO 610
600 IF E>127 GOTO 540
610 RETURN
