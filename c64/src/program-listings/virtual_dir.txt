100 REM VIRTUAL DIRECTORY - 1541
110 CLR
120 H$="0123456789ABCDEF"
130 FOR I=0 TO 5
140 READ FT$(I)
150 NEXT I
160 PRINT"{clear}VIRTUAL DIRECTORY - 1541"
170 PRINT"{down}INSERT DISKETTE IN DRIVE"
180 PRINT"{down}PRESS {rvrs on}RETURN{rvrs off} TO CONTINUE"
190 GET C$:IF C$=""THEN 190
200 IF C$<>CHR$(13)GOTO 190
210 PRINT"OK"
220 OPEN 15,8,15
230 PRINT# 15,"I0"
240 INPUT# 15,EN$,EM$,ET$,ES$
250 IF EN$="00"GOTO 300
260 PRINT"{down}"EN$", "EM$","ET$","ES$
270 CLOSE 15
280 END
290 REM FORMATTING ID
300 PRINT# 15,"M-R"CHR$(22)CHR$(0)CHR$(2)
310 GET#15,B$
320 GOSUB 1370
330 FI$=FI$+CHR$(A)
340 GET#15,B$
350 GOSUB 1370
360 FI$=FI$+CHR$(A)
370 REM BLOCKS FREE
380 PRINT# 15,"M-R"CHR$(250)CHR$(2)CHR$(3)
390 GET#15,B$
400 L=ASC(B$+CHR$(0))
410 GET#15,B$
420 GET#15,B$
430 H=ASC(B$+CHR$(0))
440 BF=L+(H*256)
450 BA=664-BF
460 OPEN 4,3
470 OPEN 2,8,2,"#"
480 OPEN 3,8,3,"$0,P,R"
490 GET#3,B$
500 DOS=ASC(B$+CHR$(0))
510 FOR I=3 TO 143
520 GET#3,B$
530 NEXT I
540 FOR I=144 TO 159
550 GOSUB 1360
560 DN$=DN$+CHR$(A)
570 NEXT I
580 GET#3,B$
590 GET#3,B$
600 FOR I=162 TO 163
610 GOSUB 1360
620 ID$=ID$+CHR$(A)
630 NEXT I
640 FOR I=164 TO 255
650 GET#3,B$
660 NEXT I
670 FOR I=1 TO 6
680 PRINT# 4
690 NEXT I
700 PRINT# 4,"DISK NAME:        "DN$
710 PRINT# 4,"DISK ID:          "ID$
720 PRINT# 4,"FORMATTING ID:    "FI$
730 PRINT# 4,"DOS TYPE:        "DOS
740 PRINT# 4,"BLOCKS ALLOCATED:"BA
750 PRINT# 4,"BLOCKS FREE:     "BF
760 PRINT# 4
770 PRINT# 4,"BLOCKS    FILE NAME     TYPE  T-S  LOAD"
780 IF F/8=INT(F/8)THEN PRINT# 4
790 GET#3,B$
800 FT=ASC(B$+CHR$(0))
810 FT$=FT$(7 AND FT)
820 GET#3,B$
830 T=ASC(B$+CHR$(0))
840 T$=RIGHT$("0"+RIGHT$(STR$(T),LEN(STR$(T))-1),2)
850 GET#3,B$
860 S=ASC(B$+CHR$(0))
870 S$=RIGHT$("0"+RIGHT$(STR$(S),LEN(STR$(S))-1),2)
880 LA$=""
890 IF(7 AND FT)<>0 AND(7 AND FT)<>2 GOTO 1020
900 PRINT# 15,"U1";2;0;T;S
910 PRINT# 15,"B-P";2;2
920 GET#2,B$
930 A=ASC(B$+CHR$(0))
940 H=INT(A/16)
950 L=A-16*H
960 LA$=MID$(H$,H+1,1)+MID$(H$,L+1,1)
970 GET#2,B$
980 A=ASC(B$+CHR$(0))
990 H=INT(A/16)
1000 L=A-16*H
1010 LA$=MID$(H$,H+1,1)+MID$(H$,L+1,1)+LA$
1020 F$=""
1030 NULL=0
1040 FOR I=1 TO 16
1050 GOSUB 1360
1060 IF B$=CHR$(0)THEN NULL=NULL+1
1070 F$=F$+CHR$(A)
1080 NEXT I
1090 IF NULL=16 GOTO 1270
1100 FOR I=1 TO 9
1110 GET#3,B$
1120 NEXT I
1130 GET#3,B$
1140 B=ASC(B$+CHR$(0))
1150 GET#3,B$
1160 B=B+256*ASC(B$+CHR$(0))
1170 B$=RIGHT$("  "+RIGHT$(STR$(B),LEN(STR$(B))-1),3)
1180 IF ST=64 THEN EOI=1
1190 IF FT<128 THEN PRINT# 4,"{rvrs on}";
1200 PRINT# 4," "B$"   "F$"  "FT$" "T$"-"S$" "LA$
1210 F=F+1
1220 IF F/8<>INT(F/8)THEN GET#3,B$:GET#3,B$
1230 GET C$:IF C$=""GOTO 1250
1240 GET C$:IF C$=""THEN 1240
1250 IF EOI=1 GOTO 1270
1260 GOTO 780
1270 CLOSE 4
1280 CLOSE 3
1290 CLOSE 2
1300 INPUT# 15,EN$,EM$,ET$,ES$
1310 CLOSE 15
1320 END
1330 REM FILE TYPES
1340 DATA DEL,SEQ,PRG,USR,REL,???
1350 REM GET A BYTE
1360 GET#3,B$
1370 IF B$=""THEN B$=CHR$(0)
1380 A=ASC(B$)
1390 IF A>127 THEN A=A-128
1400 IF A<32 OR A>95 THEN A=63
1410 IF A=34 THEN A=63
1420 RETURN
