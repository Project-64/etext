100 REM INTERROGATE A DISKETTE - 1541
110 DIM T(35)
120 FOR I=1 TO 35
130 T(I)=1
140 NEXT I
150 PRINT"{clear}INTERROGATE A DISKETTE - 1541"
160 PRINT"{down}INSERT MASTER IN DRIVE"
170 PRINT"{down}PRESS {rvrs on}RETURN{rvrs off} TO CONTINUE"
180 GET C$:IF C$=""THEN 180
190 IF C$<>CHR$(13)GOTO 180
200 PRINT"OK"
210 PRINT
220 OPEN 15,8,15
230 FOR T=1 TO 35
240 IF T(T)=0 GOTO 390
250 NS=20+2*(T>17)+(T>24)+(T>30)
260 REM SEEK
270 JOB=176
280 GOSUB 430
290 REM READ
300 FOR S=0 TO NS
310 JOB=128
320 GOSUB 430
330 IF E=1 GOTO 380
340 S$=RIGHT$(STR$(S),LEN(STR$(S))-1)
350 IF S<10 THEN S$=" "+S$
360 IF E>1 AND E<12 THEN EM$=STR$(E+18)+" READ ERROR"
370 PRINT"TRACK";T;"- SECTOR "S$" ="EM$
380 NEXT S
390 NEXT T
400 CLOSE 15
410 END
420 REM JOB QUEUE
430 TRY=0
440 PRINT# 15,"M-W"CHR$(8)CHR$(0)CHR$(2)CHR$(T)CHR$(S)
450 PRINT# 15,"M-W"CHR$(1)CHR$(0)CHR$(1)CHR$(JOB)
460 TRY=TRY+1
470 PRINT# 15,"M-R"CHR$(1)CHR$(0)
480 GET#15,E$
490 IF E$=""THEN E$=CHR$(0)
500 E=ASC(E$)
510 IF TRY=500 GOTO 540
520 IF E>127 GOTO 460
530 RETURN
540 EM$="?TIME OUT"
550 RETURN
