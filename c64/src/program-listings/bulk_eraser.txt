100 REM BULK ERASER - 1541
110 PRINT"{clear}BULK ERASER - 1541"
120 PRINT"{down}INSERT DISKETTE IN DRIVE"
130 INPUT"{down}{rvrs on}ERASE{rvrs off} THIS DISKETTE  Y{left:3}";Q$
140 IF Q$<>"Y"THEN END
150 INPUT"{down}ARE YOU SURE  Y{left:3}";Q$
160 IF Q$<>"Y"THEN END
170 OPEN 15,8,15
180 FOR I=0 TO 23
190 READ D
200 D$=D$+CHR$(D)
210 NEXT I
220 PRINT# 15,"M-W"CHR$(0)CHR$(4)CHR$(24)D$
230 FOR T=1 TO 35
240 PRINT"{home}{down:8}{rvrs on}ERASING{rvrs off} TRACK"T
250 REM SEEK
260 JOB=176
270 GOSUB 360
280 REM EXECUTE
290 JOB=224
300 GOSUB 360
310 NEXT T
320 PRINT"{home}{down:8}DONE!            "
330 CLOSE 15
340 END
350 REM JOB QUEUE
360 TRY=0
370 PRINT# 15,"M-W"CHR$(8)CHR$(0)CHR$(2)CHR$(T)CHR$(0)
380 PRINT# 15,"M-W"CHR$(1)CHR$(0)CHR$(1)CHR$(JOB)
390 TRY=TRY+1
400 PRINT# 15,"M-R"CHR$(1)CHR$(0)
410 GET#15,E$
420 IF E$=""THEN E$=CHR$(0)
430 E=ASC(E$)
440 IF TRY=500 GOTO 470
450 IF E>127 GOTO 390
460 RETURN
470 CLOSE 15
480 PRINT"{down}{rvrs on}FAILED{rvrs off}"
490 END
500 REM 21 ERROR
510 DATA  32,163,253,169, 85,141,  1, 28
520 DATA 162,255,160, 48, 32,201,253, 32
530 DATA   0,254,169,  1, 76,105,249,234
