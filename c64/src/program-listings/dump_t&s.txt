100 REM DUMP TRACK & SECTOR - 1541
110 POKE 56,159
120 CLR
130 PRINT"{clear}DUMP TRACK & SECTOR - 1541"
140 PRINT"{down}INSERT DISKETTE IN DRIVE"
150 INPUT"{down}TRACK & SECTOR (T,S)";T,S
160 IF T<1 OR T>35 GOTO 630
170 NS=20+2*(T>17)+(T>24)+(T>30)
180 IF S<0 OR S>NS GOTO 630
190 INPUT"{down}ARE YOU SURE  Y{left:3}";Q$
200 IF Q$<>"Y"GOTO 630
210 OPEN 15,8,15
220 PRINT# 15,"I0"
230 INPUT# 15,EN$,EM$,ET$,ES$
240 IF EN$="00"GOTO 290
250 PRINT"{down}"EN$","EM$","ET$","ES$
260 PRINT"{down}PRESS {rvrs on}RETURN{rvrs off} TO CONTINUE"
270 GET C$:IF C$=""THEN 270
280 IF C$<>CHR$(13)GOTO 270
290 T$=RIGHT$(STR$(T),LEN(STR$(T))-1)
300 IF T<10 THEN T$="0"+T$
310 S$=RIGHT$(STR$(S),LEN(STR$(S))-1)
320 IF S<10 THEN S$="0"+S$
330 REM SEEK
340 JOB=176
350 GOSUB 670
360 IF E=<>1 GOTO 410
370 REM READ
380 JOB=128
390 GOSUB 670
400 IF E=1 OR E=4 OR E=5 GOTO 510
410 IF E>1 AND E<12 THEN EN$=RIGHT$(STR$(E+18),2):GOTO 430
420 EN$="02":EM$="?TIME OUT":GOTO 440
430 EM$="READ ERROR"
440 ET$=T$
450 ES$=S$
460 PRINT"{down}"EN$","EM$","ET$","ES$
470 CLOSE 15
480 POKE 56,160
490 CLR
500 END
510 FOR J=0 TO 31
520 FOR I=0 TO 7
530 PRINT# 15,"M-R"CHR$(J*8+I)CHR$(4)
540 GET#15,D$
550 D=ASC(D$+CHR$(0))
560 POKE(40704+J*8+I),D
570 NEXT I
580 NEXT J
590 CLOSE 15
600 PRINT"{down}DONE!"
610 PRINT"{down}POKE56,160:CLR"
620 END
630 POKE 56,160
640 CLR
650 END
660 REM JOB QUEUE
670 TRY=0
680 PRINT# 15,"M-W"CHR$(8)CHR$(0)CHR$(2)CHR$(T)CHR$(S)
690 PRINT# 15,"M-W"CHR$(1)CHR$(0)CHR$(1)CHR$(JOB)
700 TRY=TRY+1
710 PRINT# 15,"M-R"CHR$(1)CHR$(0)
720 GET#15,E$
730 IF E$=""THEN E$=CHR$(0)
740 E=ASC(E$)
750 IF TRY=500 GOTO 770
760 IF E>127 GOTO 700
770 RETURN
