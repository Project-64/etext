10 PRINT"{clear}{down}"TAB(6)"DISK ROM TO FILE"
20 INPUT"{down}START AT LOCATION (HEX)  C100{left:6}";A$
30 Z$=A$:GOSUB 280:S=Z:IF ZF=1 GOTO 20
40 PRINT"{up}"TAB(31)Z
50 INPUT"{down}QUIT AT LOCATION (HEX)  FFFF{left:6}";A$
60 Z$=A$:GOSUB 280:Q=Z:IF ZF=1 GOTO 50
70 PRINT"{up}"TAB(31)Z
80 INPUT"{down}SAVE IN FILE NAMED  ROM 1541{left:10}";F$
90 INPUT"{down}WITH LOAD ADDRESS OF (HEX)  1100{left:6}";A$
100 Z$=A$:GOSUB 280:L=Z:IF ZF=1 GOTO 90
110 PRINT"{up}"TAB(31)Z
120 OPEN 15,8,15,"I0"
130 OPEN 1,8,5,"@0:"+F$+",P,W"
140 INPUT# 15,EN,EM$,ET,ES
150 IF EN>19 THEN PRINT"{down}DISK ERROR"EN;EM$;ET;ES:CLOSE 1:CLOSE 15:STOP
160 PRINT"{down:2}"
170 LH=INT(L/256):LL=L-256*LH
180 PRINT# 1,CHR$(LL);CHR$(LH);
190 FOR K=S TO Q
200 KH=INT(K/256):KL=K-256*KH
210 PRINT# 15,"M-R"CHR$(KL)CHR$(KH)
220 GET#15,A$:IF A$="" THEN A$=CHR$(0)
230 PRINT# 1,A$;
240 PRINT"{up}WORKING ON"K
250 NEXT
260 CLOSE 1:CLOSE 15:END
270 :
280 Z=0:ZF=0
290 IF LEN(Z$)>4 THEN ZF=1:PRINT"{down}{rvrs on}HEX STRING TOO LONG":RETURN
300 IF LEN(Z$)<4 THEN ZF=1:PRINT"{down}{rvrs on}HEX STRING TOO SHORT":RETURN
310 FOR K=1 TO 4
320 ZN=ASC(MID$(Z$,K))-48:IF ZN>9 THEN ZN=ZN-7
330 IF ZN<0 OR ZN>15 THEN ZF=1:PRINT"{down}{rvrs on}BAD HEX CHARACTER":RETURN
340 Z = Z + ZN * 16^(4-K)
350 NEXT
360 RETURN
