100 REM 1541 DISK PEEK
110 REM BY GERALD NEUFELD
120 C0=0:C2=2:C7=7:CA=10:F=15:CG=16:HO=48:HX=127
130 Z$=CHR$(0):N$=""
140 M$=" {rvrs on}    PRESS:  P TO PAUSE   Q TO QUIT    {rvrs off}"
150 PRINT"{clear}"TAB(9)"PEEK OF 1541'S MEMORY"
160 PRINT TAB(9)"{$b8:21}"
170 PRINT TAB(4)"  COPYRIGHT: G. NEUFELD, 1983"
180 PRINT"{down}        ONE MOMENT PLEASE ...."
190 DIM HX$(255),H$(15)
191 FOR K=0 TO 9:H$(K)=CHR$(48+K):NEXT:FOR K=10 TO 15:H$(K)=CHR$(55+K):NEXT
200 FOR J=0 TO F:FOR K=0 TO F:HX$(J*16+K)=H$(J)+H$(K):NEXT:NEXT
210 PRINT"{home}{down:2}"M$
220 PRINT"{down}  INPUT START ADDRESS IN HEXADECIMAL"
230 OPEN 15,8,15
240 PRINT"{down} $0000":PRINT"{up}";
250 INPUT H$
260 HL=C0:HH=C0:FOR K=1 TO 2:C=ASC(MID$(H$,K))-HO:IF C>CA THEN C=C-C7
270 IF C<C0 OR C>F THEN PRINT"{up:2}";:GOTO 240
280 D=ASC(MID$(H$,K+2))-HO:IF D>CA THEN D=D-C7
290 IF D<C0 OR D>F THEN PRINT"{up:2}";:GOTO 240
300 HH=HH+C*CG^(C2-K):HL=HL+D*CG^(C2-K):NEXT K
310 PRINT"{up}"TAB(6);
320 PRINT# 15,"M-R"CHR$(HL)CHR$(HH)CHR$(8)
330 O$="":FOR K=C0 TO C7:GET#15,A$:IF A$=N$THEN A$=Z$
340 A=ASC(A$):E=A AND HX:E$=".":IF E>31 AND E<97 THEN E$=CHR$(E)
350 O$=O$+E$:PRINT" "HX$(ASC(A$));:NEXT:PRINT" {rvrs on}"O$
360 FL=0:HL=HL+8:IF HL>255 THEN HL=HL-256:HH=HH+1:FL=1:PRINT M$
370 IF HL=128 THEN FL=1:PRINT M$
380 PRINT" $"HX$(HH)HX$(HL);:IF FL=1 THEN PRINT:PRINT"{up}";:GOTO 250
390 GET A$:IF A$=""GOTO 320
400 IF A$="P"THEN PRINT:PRINT"{up}";:GOTO 250
410 CLOSE 15
