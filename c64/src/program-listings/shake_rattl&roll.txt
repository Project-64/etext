100 REM SHAKE, RATTLE, & ROLL - 1541
110 PRINT"{clear}SHAKE, RATTLE, & ROLL - 1541"
120 PRINT"{down}INSERT DISKETTE IN DRIVE"
130 INPUT"{down}CLATTER TRACK";T
140 IF T<1 OR T>35 THEN END
150 INPUT"{down}ARE YOU SURE  Y{left:3}";Q$
160 IF Q$<>"Y"THEN END
170 OPEN 15,8,15
180 OPEN 2,8,2,"#"
190 PRINT"{clear}"
200 REM SEEK
210 GOSUB 360
220 NS=20+2*(T>17)+(T>24)+(T>30)
230 FOR S=0 TO NS
240 REM READ
250 PRINT# 15,"U1";2;0;T;S
260 INPUT# 15,EN$,EM$,ET$,ES$
270 PRINT"TRACK";T;"- ";
280 IF EN$="00"THEN PRINT"SECTOR";S;"= OK":GOTO 300
290 PRINT"SECTOR "ES$" = "EN$" "EM$
300 NEXT S
310 CLOSE 2
320 INPUT# 15,EN$,EM$,ET$,ES$
330 CLOSE 15
340 END
350 REM JOB QUEUE
360 TRY=0
370 PRINT# 15,"M-W"CHR$(8)CHR$(0)CHR$(2)CHR$(T)CHR$(S)
380 PRINT# 15,"M-W"CHR$(1)CHR$(0)CHR$(1)CHR$(176)
390 TRY=TRY+1
400 PRINT# 15,"M-R"CHR$(1)CHR$(0)
410 GET#15,E$
420 IF E$=""THEN E$=CHR$(0)
430 IF TRY=500 GOTO 460
440 IF ASC(E$)>127 GOTO 390
450 RETURN
460 CLOSE 2
470 INPUT# 15,EN$,EM$,ET$,ES$
480 CLOSE 15
490 PRINT"{down}{rvrs on}FAILED{rvrs off}"
500 END
