100 REM INTERROGATE FORMATTING ID'S - 1541
110 DIM T(35)
120 FOR I=1 TO 35
130 T(I)=1
140 NEXT I
150 PRINT"{clear}INTERROGATE FORMATTING ID'S - 1541"
160 PRINT"{down}INSERT MASTER IN DRIVE"
170 PRINT"{down}PRESS {rvrs on}RETURN{rvrs off} TO CONTINUE"
180 GET C$:IF C$=""THEN 180
190 IF C$<>CHR$(13)GOTO 180
200 OPEN 15,8,15
210 PRINT"{clear}"
220 REM SEEK
230 FOR T=1 TO 35
240 IF T(T)=0 GOTO 440
250 GOSUB 550
260 IF E<>1 GOTO 410
270 PRINT# 15,"M-R"CHR$(22)CHR$(0)
280 GET#15,I$
290 IF I$=""THEN I$=CHR$(0)
300 I=ASC(I$)
310 I$=RIGHT$(STR$(I),LEN(STR$(I))-1)
320 PRINT# 15,"M-R"CHR$(23)CHR$(0)
330 GET#15,D$
340 IF D$=""THEN D$=CHR$(0)
350 D=ASC(D$)
360 D$=RIGHT$(STR$(D),LEN(STR$(D))-1)
370 I$="CHR$("+I$+")"
380 D$="CHR$("+D$+")"
390 ID$=I$+" + "+D$
400 GOTO 450
410 IF E=3 THEN ID$="?NO SYNC MARKS":GOTO 450
420 IF E=2 THEN ID$="?HEADER BLOCKS NOT PRESENT":GOTO 450
430 IF E=9 THEN ID$="?CHECKSUM ERROR IN HEADERS":GOTO 450
440 ID$="?TIME OUT"
450 T$=RIGHT$(STR$(T),LEN(STR$(T))-1)
460 IF T<10 THEN T$=" "+T$
470 PRINT"TRACK "T$" = "ID$
480 REM PAUSE
490 GET C$:IF C$=""GOTO 510
500 GET C$:IF C$=""THEN 500
510 NEXT T
520 CLOSE 15
530 END
540 REM JOB QUEUE
550 TRY=0
560 PRINT# 15,"M-W"CHR$(8)CHR$(0)CHR$(2)CHR$(T)CHR$(0)
570 PRINT# 15,"M-W"CHR$(1)CHR$(0)CHR$(1)CHR$(176)
580 TRY=TRY+1
590 PRINT# 15,"M-R"CHR$(1)CHR$(0)
600 GET#15,E$
610 IF E$=""THEN E$=CHR$(0)
620 E=ASC(E$)
630 IF TRY=500 GOTO 650
640 IF E>127 GOTO 580
650 RETURN
