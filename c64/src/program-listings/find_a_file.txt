100 REM FIND A FILE
110 PRINT"{clear}FIND A FILE - 1541"
120 PRINT"{down}INSERT DISKETTE IN DRIVE"
130 PRINT"{down}PRESS {rvrs on}RETURN{rvrs off} TO CONTINUE"
140 GET C$:IF C$=""THEN 140
150 IF C$<>CHR$(13)GOTO 140
160 PRINT"OK"
170 OPEN 15,8,15
180 PRINT# 15,"I0"
190 INPUT# 15,EN$,EM$,ET$,ES$
200 IF EN$="00"GOTO 240
210 PRINT"{down}"EN$", "EM$","ET$","ES$
220 CLOSE 15
230 END
240 INPUT"{down}FILENAME";F$
250 IF LEN(F$)<>0 AND LEN(F$)<17 GOTO 280
260 CLOSE 15
270 END
280 OPEN 2,8,2,"0:"+F$+",?,R"
290 INPUT# 15,EN$,EM$,ET$,ES$
300 IF EN$="00"GOTO 320
310 GOTO 530
320 PRINT# 15,"M-R"CHR$(97)CHR$(2)
330 GET#15,D$
340 D=ASC(D$+CHR$(0))
350 PRINT# 15,"M-R"CHR$(24)CHR$(0)CHR$(2)
360 GET#15,T$
370 T=ASC(T$+CHR$(0))
380 GET#15,S$
390 S=ASC(S$+CHR$(0))
400 D$=RIGHT$(STR$(D),LEN(STR$(D))-1)
410 IF D<10 THEN D$="0"+D$
420 T$=RIGHT$(STR$(T),LEN(STR$(T))-1)
430 IF T<10 THEN T$="0"+T$
440 S$=RIGHT$(STR$(S),LEN(STR$(S))-1)
450 IF S<10 THEN S$="0"+S$
460 PRINT"{down}TRACK 18 - SECTOR "D$
470 PRINT"{down}TRACK "T$" - SECTOR "S$
480 CLOSE 2
490 INPUT# 15,EN$,EM$,ET$,ES$
500 CLOSE 15
510 PRINT"{down}DONE!"
520 END
530 PRINT"{down}"EN$", "EM$","ET$","ES$
540 CLOSE 2
550 INPUT# 15,EN$,EM$,ET$,ES$
560 CLOSE 15
570 PRINT"{down}{rvrs on}FAILED{rvrs off}"
580 END
