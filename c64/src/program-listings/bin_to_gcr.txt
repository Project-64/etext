100 REM CONVERT BINARY TO GCR
110 PRINT"{clear}ENTER FOUR BYTES (DECIMAL){down}"
120 B$(0)="0":B$(1)="1":FOR K=0 TO 7:P(K)=2^K:NEXT
130 FOR K=0 TO 7:P(K)=2^K:NEXT
140 OPEN 15,8,15
150 :
160 REM INPUT BYTES & STORE IN DISK RAM ($52/5)
170 FOR K=0 TO 3
180 PRINT"BYTE#"K"=";:INPUT X
190 IF  X<0 OR X>255 GOTO 180
200 PRINT"{up}"TAB(18);:GOSUB 430
210 PRINT# 15,"M-W"CHR$(82+K)CHR$(0)CHR$(1)CHR$(X)
220 NEXT
230 :
240 REM SET UP POINTER TO STORAGE AREA ($30/31)
250 PRINT# 15,"M-W"CHR$(48)CHR$(0)CHR$(2)CHR$(0)CHR$(3)
260 :
270 REM SET UP GCR POINTER ($34)
280 PRINT# 15,"M-W"CHR$(52)CHR$(0)CHR$(1)CHR$(0)
290 :
300 REM EXECUTE PUT4GB ($F6D0) IPC ROUTINE
310 PRINT# 15,"M-E"CHR$(208)CHR$(246)
320 :
330 REM PEEK OUT AND DISPLAY RESULTS
340 PRINT# 15,"M-R"CHR$(00)CHR$(3)CHR$(5)
350 PRINT"{down}THE FIVE EQUIVALENT GCR BYTES ARE:{down}"
360 FOR K=1 TO 5
370 GET#15,X$:X=ASC(X$+CHR$(0))
380 PRINT"BYTE#"K"="X;TAB(18);:GOSUB 430
390 NEXT
400 CLOSE 15:END
410 :
420 SUB TO DISPLAY BINARY EQUI VALENTS
430 PRINT"%";
440 FOR L=7 TO 0 STEP-1
450 T=INT(X/2^L)
460 X=X-T*P(L)
470 PRINT B$(T);
480 NEXT:PRINT:RETURN
