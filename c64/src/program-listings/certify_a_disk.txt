100 REM CERTIFY A DISKETTE - 1541
110 FOR I=1 TO 32
120 NULL$=NULL$+CHR$(0)
130 WRITE$=WRITE$+CHR$(15)
140 NEXT I
150 DIM T%(681),S%(681)
160 PRINT"{clear}           CERTIFY A DISKETTE"
170 PRINT"{down}                {rvrs on}WARNING{rvrs off}"
180 PRINT"{down}{rvrs on}RANDOM ACCESS{rvrs off} AND {rvrs on}DEL{rvrs off} FILES WILL BE LOST"
190 PRINT"REMOVE {rvrs on}WRITE PROTECT TAB{rvrs off}"
200 PRINT"{down}INSERT DISKETTE IN DRIVE"
210 PRINT"{down}PRESS {rvrs on}RETURN{rvrs off} TO CONTINUE"
220 GET C$:IF C$=""THEN 220
230 IF C$<>CHR$(13)GOTO 220
240 PRINT"OK"
250 OPEN 15,8,15
260 PRINT# 15,"I0"
270 INPUT# 15,EN$,EM$,ET$,ES$
280 IF EN$="00"GOTO 330
290 PRINT"{down}"EN$", "EM$","ET$","ES$
300 CLOSE 15
310 END
320 REM BAM
330 PRINT# 15,"M-R"CHR$(0)CHR$(7)CHR$(192)
340 FOR I=0 TO 191
350 GET#15,B$
360 IF B$=""THEN B$=CHR$(0)
370 BAM$=BAM$+B$
380 NEXT I
390 DOS=ASC(MID$(BAM$,3,1))
400 IF DOS=65 GOTO 460
410 CLOSE 15
420 PRINT"{down}73,CBM DOS V2.6 1541,00,00"
430 PRINT"{down}{rvrs on}FAILED{rvrs off}"
440 END
450 REM BUFFER
460 I=0
470 FOR J=1 TO 8
480 PRINT# 15,"M-W"CHR$(I)CHR$(4)CHR$(32)WRITE$
490 I=I+32
500 NEXT J
510 T=1
520 S=0
530 C=0
540 A=0
550 PRINT# 15,"B-A";0;T;S
560 INPUT# 15,EN$,EM$,ET$,ES$
570 IF EN$="00"GOTO 620
580 T=VAL(ET$)
590 IF T=0 AND C=0 GOTO 760
600 IF T=0 GOTO 800
610 S=VAL(ES$)
620 T$=RIGHT$("0"+RIGHT$(STR$(T),LEN(STR$(T))-1),2)
630 S$=RIGHT$("0"+RIGHT$(STR$(S),LEN(STR$(S))-1),2)
640 C=C+1
650 IF C=1 THEN PRINT"{up}  "
660 PRINT# 15,"B-A";0;T;S
670 PRINT"{home}{down:6}{rvrs on}CERTIFYING{rvrs off} TRACK ";T$;" - SECTOR ";S$
680 PRINT"{down}NUMBER OF SECTORS CERTIFIED    :";C
690 PRINT"{down}NUMBER OF BAD SECTORS ALLOCATED:";A
700 GOSUB 1030
710 IF E=1 GOTO 550
720 A=A+1
730 T%(A)=T
740 S%(A)=S
750 GOTO 550
760 CLOSE 15
770 PRINT"{down}ALL SECTORS HAVE BEEN ALLOCATED"
780 PRINT"{down}{rvrs on}FAILED{rvrs off}"
790 END
800 I=0
810 FOR J=1 TO 6
820 PRINT# 15,"M-W"CHR$(I)CHR$(4)CHR$(32)MID$(BAM$,I+1,32)
830 I=I+32
840 NEXT J
850 PRINT# 15,"M-W"CHR$(192)CHR$(4)CHR$(32)NULL$
860 PRINT# 15,"M-W"CHR$(224)CHR$(4)CHR$(32)NULL$
870 T=18
880 S=0
890 GOSUB 1030
900 PRINT# 15,"I0"
910 INPUT# 15,EN$,EM$,ET$,ES$
920 IF A<>0 GOTO 960
930 CLOSE 15
940 PRINT"{down}NO BAD SECTORS!"
950 END
960 FOR I=1 TO A
970 PRINT# 15,"B-A";0;T%(I);S%(I)
980 NEXT I
990 CLOSE 15
1000 PRINT"{down}DONE!"
1010 END
1020 REM SEEK
1030 JOB=176
1040 GOSUB 1120
1050 IF E=1 GOTO 1080
1060 RETURN
1070 REM WRITE
1080 JOB=144
1090 GOSUB 1120
1100 RETURN
1110 REM JOB QUEUE
1120 TRY=0
1130 PRINT# 15,"M-W"CHR$(8)CHR$(0)CHR$(2)CHR$(T)CHR$(S)
1140 PRINT# 15,"M-W"CHR$(1)CHR$(0)CHR$(1)CHR$(JOB)
1150 TRY=TRY+1
1160 PRINT# 15,"M-R"CHR$(1)CHR$(0)
1170 GET#15,E$
1180 IF E$=""THEN E$=CHR$(0)
1190 E=ASC(E$)
1200 IF TRY=500 GOTO 1220
1210 IF E>127 GOTO 1150
1220 RETURN
