100 REM COPY TRACK & SECTOR - 1541
110 PRINT"{clear}COPY TRACK & SECTOR - 1541"
120 PRINT"{down}INSERT MASTER IN DRIVE"
130 INPUT"{down}TRACK AND SECTOR (T,S)";T,S
140 IF T<1 OR T>35 THEN END
150 NS=20+2*(T>17)+(T>24)+(T>30)
160 IF S<0 OR S>NS THEN END
170 INPUT"{down}ARE YOU SURE  Y{left:3}";Q$
180 IF Q$<>"Y"THEN END
190 OPEN 15,8,15
200 PRINT# 15,"I0"
210 INPUT# 15,EN$,EM$,ET$,ES$
220 IF EN$="00"GOTO 270
230 PRINT"{down}"EN$", "EM$","ET$","ES$
240 CLOSE 15
250 END
260 REM SEEK
270 JOB=176
280 GOSUB 570
290 IF E=1 GOTO 320
300 GOTO 690
310 REM READ
320 JOB=128
330 GOSUB 570
340 IF E=1 GOTO 360
350 GOTO 690
360 CLOSE 15
370 PRINT"{down}INSERT CLONE IN DRIVE"
380 PRINT"PRESS {rvrs on}RETURN{rvrs off} TO CONTINUE"
390 GET C$:IF C$=""THEN 390
400 IF C$<>CHR$(13)GOTO 390
410 PRINT"OK"
420 OPEN 15,8,15
430 REM SEEK
440 JOB=176
450 GOSUB 570
460 IF E=1 GOTO 490
470 GOTO 690
480 REM WRITE
490 JOB=144
500 GOSUB 570
510 IF E=1 GOTO 530
520 GOTO 690
530 CLOSE 15
540 PRINT"{down}DONE!"
550 END
560 REM JOB QUEUE
570 TRY=0
580 PRINT# 15,"M-W"CHR$(8)CHR$(0)CHR$(2)CHR$(T)CHR$(S)
590 PRINT# 15,"M-W"CHR$(1)CHR$(0)CHR$(1)CHR$(JOB)
600 TRY=TRY+1
610 PRINT# 15,"M-R"CHR$(1)CHR$(0)
620 GET#15,E$
630 IF E$=""THEN E$=CHR$(0)
640 E=ASC(E$)
650 IF TRY=500 GOTO 690
660 IF E>127 GOTO 600
670 RETURN
680 REM ERROR HANDLER
690 ET$=RIGHT$(STR$(T),LEN(STR$(T))-1)
700 IF T<10 THEN ET$="0"+ET$
710 ES$=RIGHT$(STR$(S),LEN(STR$(S))-1)
720 IF S<10 THEN ES$="0"+ES$
730 IF E>1 AND E<12 THEN EN$=RIGHT$(STR$(E+18),2):GOTO 750
740 EN$="02":EM$="?TIME OUT":GOTO 770
750 IF E=7 OR E=8 THEN EM$="WRITE ERROR":GOTO 770
760 EM$="READ ERROR"
770 PRINT"{down}"EN$", "EM$","ET$","ES$
780 PRINT"{down}{rvrs on}FAILED{rvrs off}"
790 CLOSE 15
800 END
